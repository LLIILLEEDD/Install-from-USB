@echo off
:: –°–∫—Ä–∏–ø—Ç —Å–æ–∑–¥–∞–Ω –¥–ª—è –ø–µ—Ä–µ–Ω–æ—Å–∞ —Ñ–∞–π–ª–æ–≤ —Ñ–ª–µ—à–∫–∏ –≤ –∫–æ—Ä–µ–Ω—å –¥–∏—Å–∫–∞ –∏ –ø–æ—Å–ª–µ–¥—É—é—É—â—É—é –∞–≤—Ç–æ–º–∞—Ç–∏—á–µ—Å–∫—É—é —É—Å—Ç–∞–Ω–æ–≤–∫—É –Ω–µ–∫–æ—Ç–æ—Ä—ã—Ö –ø—Ä–æ–≥—Ä–∞–º–º. –ó–∞–ª–∏—Ç –Ω–∞ –ì–∏—Ç—Ö–∞–±–µ. 
:: –¢—É—Ç –µ—Å—Ç—å –µ—â–µ —á—Ç–æ –¥–æ–ø–∏–ª–∏–≤–∞—Ç—å, –Ω–æ —É–∂–µ —Å–∫—Ä–∏–ø—Ç –∞–∫—Ç–∏–≤–Ω–æ –∏—Å–ø–æ–ª—å–∑—É–µ—Ç—Å—è –≤ –º–æ–µ–π –ø—Ä–∞–∫—Ç–∏–∫–µ.
:: The script was created to transfer files from a flash drive to the root of the disk and then automatically install some programs. Uploaded to Github.
:: There is still something to be completed here, but the script is already actively used in my practice.

:: –ù–∞—Ö–æ–¥–∏–º –±—É–∫–≤—É –¥–∏—Å–∫–∞ —Ñ–ª–µ—à–∫–∏
:: Find the drive letter of the flash drive
for /f "tokens=2 delims==" %%D in ('wmic logicaldisk where drivetype^=2 get deviceid /value') do (
    set "flashDrive=%%D"
    goto :continue
)
:continue

if not defined flashDrive (
    echo [31mFlash drive not found[0m
    exit /b
)

:: –û–ø—Ä–µ–¥–µ–ª—è–µ–º –ø—É—Ç–∏
:: Defining paths
set "sourceFolder=%flashDrive%\install"
set "targetFolder=C:\install"
set "installLogFile=%targetFolder%\InstallLog.txt"
set "desktopPath=%USERPROFILE%\Desktop"
set "desktopLogFile=%desktopPath%\InstallLog.txt"

:: –°–æ–∑–¥–∞–µ–º –ø–∞–ø–∫—É install –Ω–∞ C:\ –¥–∏—Å–∫–µ
:: Create an install folder on the C:\ drive
if not exist "%targetFolder%" (
    mkdir "%targetFolder%"
    echo C:\install folder created
)

:: –î–æ–±–∞–≤–ª—è–µ–º –≤ Windows Defender –∏—Å–∫–ª—é—á–µ–Ω–∏–µ
:: Add exceptions to Windows Defender
powershell -Command Add-MpPreference -ExclusionPath "%targetFolder%" -Force
echo Exception added to Windows Defender

echo.
echo.
echo.
echo         [33mLEEETS GOOOO[0m
echo.
echo.
echo.

:: –ö–æ–ø–∏—Ä—É–µ–º —Ñ–∞–π–ª—ã —Å —Ñ–ª–µ—à–∫–∏ –≤ —Ç–∞—Ä–≥–µ—Ç
:: Copy files from the flash drive to the target
xcopy "%sourceFolder%\*" "%targetFolder%\" /s /e /i /h /y
echo.
echo.

:: –õ–æ–≥–∏—Ä—É–µ–º —Å–æ–∑–¥–∞–Ω–∏–µ –ø–∞–ø–∫–∏
:: Logging the creation of a folder
call :Log "Files successfully copied from %sourceFolder% to %targetFolder%"
echo.

:: –£—Å—Ç–∞–Ω–æ–≤–∫–∞ –ø—Ä–æ–≥—Ä–∞–º–º
:: Installing programs
call :InstallProgram "7zipSetup.exe" "/S" "7-Zip"
call :InstallProgram "Notepad.8.6.4.exe" "/S /I" "Notepad++"
call :InstallProgram "AdobeAcrobat.exe" "/S" "Adobe Acrobat Reader"
call :InstallProgram "ChromeSetup.exe" "/silent /install" "Google Chrome"
call :InstallProgram "Driver.Booster.exe" "/SILENT" "Driver Booster"
call :InstallProgram "FirefoxSetup.msi" "/q" "Mozilla Firefox"
call :InstallProgram "FSViewerSetup78.exe" "/S" "FastStone Image Viewer"
call :InstallProgram "Flameshot-12.1.0-win64.msi" "/qn" "Flameshot"
call :InstallProgram "JavaSetup.exe" "/s" "Java"
call :InstallProgram "naps2.msi" "/q" "Naps2"
call :InstallProgram "PDF-XChange.exe" "/S /I" "PDF-XChange"
call :InstallProgram "ThunderbirdSetup.exe" "/S" "Mozilla Thunderbird"
call :InstallProgram "vlcwin64.exe" "/S" "VLC Media Player"
echo.
echo.
echo.
echo         [32mWELL DONE BRO[0m
echo.
echo.
echo.
pause
exit /b

:InstallProgram
"%targetFolder%\%~1" %~2
if errorlevel 1 (
    call :Log "Error during installation of %3."
) else (
    call :Log "%~3 installed successfully."
)

goto :eof

:Log
echo %DATE% %TIME% - %* >> "%installLogFile%"
echo %DATE% %TIME% - %* >> "%desktopLogFile%"
echo %DATE% %TIME% - %*

goto :eof

:: –ê–≤—Ç–æ—Ä: LLIILLEEDD
:: Author: LLIILLEEDD
